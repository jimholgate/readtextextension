<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="TTS_Config" script:language="StarBasic">Rem *  *  *  *  * BASIC *  *  *  *  * 
Option Explicit
&apos; Variable holding the dialog box.
Private f_dlgSpeechSetup As Variant
Private f_dlgAboutReadSelection As Variant
&apos; Persistent information about the current recording track and 
&apos; the URL of last processed document.
global s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle As String
global i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack As Integer
global s_CaBcVancouverHolgateJamesReadtextextensionWorkingCountryLanguage As String
global b_CaBcVancouverHolgateJamesReadtextextensionHideExports As Boolean
global i_CaBcVancouverHolgateJamesReadtextextensionWinItunesVersion As Integer
&apos;&apos; # Configuration tools
&apos;&apos;  
&apos;&apos; These procedures make dialogues, get settings and write settings.
&apos;&apos; Dialogues in Read Text Extension are updated each time that you
&apos;&apos; see them.  You can immediately change the dialogue language from
&apos;&apos; the display preferences field of the main dialogue.
&apos;&apos; 

Sub ClearWorkingCountryLanguage()
	s_CaBcVancouverHolgateJamesReadtextextensionWorkingCountryLanguage = &quot;&quot;
End Sub

Sub SetWorkingCountryLanguage()
	s_CaBcVancouverHolgateJamesReadtextextensionWorkingCountryLanguage = fsSelectionLanguageAndRegion()
End Sub

Function fiMyCurrentAudioTrack( _
	ByVal bIncrement) As Integer
&apos;&apos; # Audio tracks
&apos;&apos;
&apos;&apos; An mp3 album can have up to 255 tracks.  This extension uses
&apos;&apos; the document *title* as the album title.  The track number is
&apos;&apos; the number of times you have created a sound file from a 
&apos;&apos; document with the same title. if the track number is over
&apos;&apos; 255, then the counter restarts at 1.
&apos;&apos;
&apos;&apos; Title vs. file name
&apos;&apos; -------------------
&apos;&apos;
&apos;&apos; The document title is *not* the same as the file name, and
&apos;&apos; any number of files can have the same title.  The title is
&apos;&apos; used as the album name of exported sound files.  You can
&apos;&apos; assemble an album made up of different documents, by setting
&apos;&apos; the title of each document to the same value.  Use
&apos;&apos; *File - Properties - Description - Title* to set a document
&apos;&apos; title.
&apos;&apos;    
	fiMyCurrentAudioTrack = 1
	On error Goto fiMyCurrentAudioTrackErr
	If bIncrement Then
		&apos; Add one to the track index
		If s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle = fsThisDocTitle Then
			If i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack &lt; 256 And _
				i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack &gt; 0 Then
				i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack = _
				i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack + 1
			Else
				i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack = 1
			End If
		Else
			i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack = 1
			s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle = fsThisDocTitle
		End If
	Else
		If Not(s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle = fsThisDocTitle) Then
			i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack = 1
			s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle = fsThisDocTitle
		End If
	End If
	fiMyCurrentAudioTrack = i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack
	Exit Function
	fiMyCurrentAudioTrackErr:
	
	fiMyCurrentAudioTrack = 1
	i_CaBcVancouverHolgateJamesReadtextextensionLastSpokenTrack = 1
	s_CaBcVancouverHolgateJamesReadtextextensionLastSpokenDocTitle = fsThisDocTitle
End Function


Sub ConfigureReadTextAloud()
	Dim sFile As String
	Dim msgs() As Variant
	Dim errorCode As Integer
	Dim config(fiCountConfigOptions) As Variant
	Dim bHideExports As Boolean
	bHideExports = False
	
	&apos; Open up the dialog
	CheckUnixSpeech()
	ClearWorkingCountryLanguage()
	DialogLibraries.LoadLibrary(&quot;textToSpeech&quot;)
	f_dlgSpeechSetup = CreateUnoDialog(DialogLibraries.textToSpeech.Dialog1 )
	Select Case fsGetOS()
		Case &quot;MacOS&quot;
		&apos; New versions of MacOS restrict third party apps from writing
		&apos; media files, so the extension hides export options in the main
		&apos; dialogue.  High Sierra and newer versions of MscOS do not 
		&apos; include a built-in telnet command.
		If Not( _
			fileexists( _
			&quot;/usr/bin/telnet&quot;)) Then
			Select Case fsGetMyTunesApp()
				Case &quot;Music&quot;
				&apos; MacOS 11
				bHideExports = False
				Case &quot;iTunes&quot;
				bHideExports = False
				Case Else
				bHideExports = True
			End Select
		End If
	End Select
	b_CaBcVancouverHolgateJamesReadtextextensionHideExports = bHideExports
	With f_dlgSpeechSetup
		If fbHelpIsInstalled Then
			.model.HelpURL = fsAppSignature &amp; &quot;/set_up.xhp&quot;
		Else
			.model.HelpURL = &quot;&quot;
		End If
		&apos;Load default settings, so any settings undefined by settings file are not empty
		config() = fvTts_config_createDefaultConfig(msgs(), errorCode)
		&apos;Load the settings you used last time
		sFile = fsFullPathOf(fsExtensionSettingsIni)
		If fileexists(sFile) Then
			tts_config_parseFile(sFile, config(), errorCode)
		Else
			&apos; The &quot;About&quot; dialog needs this file to show current information
			tts_config_writeFile(config(), sfile)
		End If
		&apos;Now apply the settings to the dialog box
		.GetControl(&quot;CheckBox1&quot;).Model.State = config(fiAlwaysShowDialogCheckBox())
		If fbEnhancedRTVersion And fbClipBoardAvailable Then
			.GetControl(&quot;CheckBox2&quot;).Model.State = config(fiSmartSelectCheckBox())
		Else
			.GetControl(&quot;CheckBox2&quot;).Model.State = 0
			.GetControl(&quot;CheckBox2&quot;).Visible = False
		End If
		ttsUpdateCombo2a(False)
		&apos;Update the labels with your language
		ttsUpdateLabels
		.GetControl(&quot;ComboBox1&quot;).Model.Text = config(fiFestivalComboChoice())
		.GetControl(&quot;ComboBox2&quot;).Model.Text = config(fiExternalCommand())
		.GetControl(&quot;ComboBox3&quot;).Model.Text = config(fiDialogLanguageCombo())
		.GetControl(&quot;ComboBox4&quot;).Model.Text = config(fiHtmlComboURL())
		.GetControl(&quot;FileControl1&quot;).Model.Text = config(fiFestivalPath())
		.GetControl(&quot;FileControl2&quot;).Model.Text = config(fiExternalProgramPath)
		.GetControl(&quot;OptionButton1&quot;).Model.State = config(fiFestivalOption())
		.GetControl(&quot;OptionButton2&quot;).Model.State = config(fiExternalOption())
		.GetControl(&quot;OptionButton3&quot;).Model.State = config(fiHtmlOption)
		
		ttsUpdateCombo1()
		
		&apos;Default choices are for custom scripts or apps - Win, Mac, Linux, Unix etc.
		
		ttsUpdateDisplay
		&apos; Stop Linux or MacOS speech daemon from continuing to speak
		&apos; or delete the Windows temporary lock file so speech ends.
		&apos; The speech only stops if you use the one of the scripts shown
		&apos; by default in the extension dialogue.
		If fileexists(sFile) Or fileexists(fsMyTempLock(&quot;lock&quot;)) Then
			If .GetControl(&quot;OptionButton2&quot;).Model.State Then
				resetSpeechDispatcher(True)
			ElseIf .GetControl(&quot;OptionButton1&quot;).Model.State Then
				If fileexists(&quot;/usr/bin/killall&quot;) Or fileexists(&quot;/usr/local/bin/killall&quot;) Then
					If fileexists(&quot;/usr/bin/festival&quot;) Then
						Shell(&quot;killall&quot;, 0, &quot;festival&quot;, True)
					ElseIf fileexists(&quot;/usr/bin/flite&quot;) Then
						Shell(&quot;killall&quot;, 0, &quot;flite&quot;, True)
					End If
				End If
				If fileexists(fsMyTempLock(&quot;lock&quot;)) Then
					kill fsMyTempLock()
				End If
			End If
		End If
		If .Execute() &lt;&gt; 0 Then
			&apos;Replace the old settings file
			config() = fvCurrentDialogConfig()
			.Dispose()
			If fileexists(sFile) Then
				kill sFile
			End If
			tts_config_writeFile(config(), sfile)
			&apos;Call the shell to execute the program specified with the commands specified
			ttsActuate(config(), _
			msgs(), _
			errorCode, _
			&quot;&quot;)
		End If
	End With
End Sub


Sub ttsUpdateCombo1()
	dialog1Combo1ShowFestivalScripts(f_dlgSpeechSetup)
End Sub


Sub ttsUpdateCombo2()
	ttsUpdateCombo2a(True)
End Sub


Sub ttsUpdateCombo2F()
	ttsUpdateCombo2a(False)
End Sub


Sub ttsUpdateCombo2a( _
	bRefreshText)
	If InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text), _
		&quot;\wscript.exe&quot;) &gt; 0 Then
		dialog1Combo2ShowSAPIChoices(f_dlgSpeechSetup)
		&apos; Typical for Mac
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;bin/say&quot;) &gt; 0 Then
		dialog1Combo2ShowSAYChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;bin/osascript&quot;) &gt; 0 Then
		dialog1Combo2ShowAppleScriptChoices(f_dlgSpeechSetup)
		&apos; Typical for Linux
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;speak-ng&quot;) &gt; 0 Then
		dialog1Combo2ShowESPEAKChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;espeak-ng&quot;) &gt; 0 Then
		dialog1Combo2ShowESPEAKChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;espeak&quot;) &gt; 0 Then
		dialog1Combo2ShowESPEAKChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;festival&quot;) &gt; 0 Then
		dialog1Combo2ShowFESTIVALChoices(f_dlgSpeechSetup)
		&apos;Festival text2wav
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;text2wave&quot;) &gt; 0 Then
		dialog1Combo2ShowTEXT2WAVEChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;/bin/python&quot;) &gt; 0 Then
		fiDialog1Combo2ShowMyPythonChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;python.exe&quot;) &gt; 0 Then
		fiDialog1Combo2ShowMyPythonChoices(f_dlgSpeechSetup)
	ElseIf InStr(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text),&quot;/bin/open&quot;) &gt; 0 And _
		fsGetOS() = &quot;MacOS&quot; Then
		&apos;The system chooses an application to open the file or URL (See man: gnome-open and xdg-open)
		&apos;There are some examples at https://ss64.com/MacOS/open.html and 
		&apos;https://developer.apple.com/library/mac/#documentation/Darwin/Reference/ManPages/man1/open.1.html
		dialog1Combo2ShowMEDIAChoices(f_dlgSpeechSetup)
		&apos; You entered a media player
	ElseIf fbIsAMediaPlayer(LCase(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text)) Then
		dialog1Combo2ShowMEDIAChoices(f_dlgSpeechSetup)
	Else
		
	End If
	If bRefreshText And FileExists(f_dlgSpeechSetup.GetControl(&quot;FileControl2&quot;).Model.Text) Then
		f_dlgSpeechSetup.GetControl(&quot;ComboBox2&quot;).Model.Text = f_dlgSpeechSetup.GetControl(&quot;ComboBox2&quot;).GetItem(0)
	End If
End Sub


Sub ttsUpdateLabels()
	Dim s1 As String
	UpdatePoSettingsDone =  - 1
	s1 = fsDisplayLanguage()
	&apos;Update the labels with your language
	With f_dlgSpeechSetup
		.GetControl(&quot;CheckBox1&quot;).model.Label = fsLookUpTerm2(&quot;s_always-show-this-dialog&quot;, s1)
		.GetControl(&quot;CheckBox2&quot;).model.Label = fsLookUpTerm2(&quot;s_touch-screen&quot;, s1)
		.GetControl(&quot;CommandButton1&quot;).model.Label = fsLookUpTerm2(&quot;s_ok&quot;, s1)
		.GetControl(&quot;CommandButton2&quot;).model.Label = fsLookUpTerm2(&quot;s_cancel&quot;, s1)
		.GetControl(&quot;CommandButton3&quot;).model.Label = fsLookUpTerm2(&quot;s_about&quot;, s1) &amp; &quot;...&quot;
		.GetControl(&quot;FrameControl1&quot;).model.Label = fsLookUpTerm2(&quot;s_external-program&quot;, s1)
		.GetControl(&quot;FrameControl2&quot;).model.Label = fsLookUpTerm2(&quot;s_display&quot;, s1)
		.GetControl(&quot;Label1&quot;).model.Label = fsLookUpTerm2(&quot;s_script-commands&quot;, s1)
		.GetControl(&quot;Label2&quot;).model.Label = fsLookUpTerm2(&quot;s_command-line-options&quot;, s1)
		.GetControl(&quot;OptionButton1&quot;).model.Label = fsLookUpTerm2(&quot;s_use-festival-with-a-script&quot;, s1)
		.GetControl(&quot;OptionButton2&quot;).model.Label = fsLookUpTerm2(&quot;s_external-program&quot;, s1)
		.GetControl(&quot;OptionButton3&quot;).model.Label = fsLookUpTerm2(&quot;s_use-a-web-application&quot;, s1)
		.model.Title = fsLookUpTerm2(&quot;s_read-text&quot;, s1)
	End With
End Sub


Sub ttsUpdateDisplay()
	&apos; These dialog items toggle between enabled And disabled depending On option button selected
	With f_dlgSpeechSetup
		Dim i#
		i = Len(.GetControl(&quot;FileControl1&quot;).Model.Text)
		&apos; If festival is installed, then enable the festival option in `Dialog1`.
		If i = 0 Then
			&apos; No text in file control box
			.GetControl(&quot;OptionButton1&quot;).model.enabled = False
			.GetControl(&quot;ComboBox1&quot;).model.enabled = False
			.GetControl(&quot;FileControl1&quot;).model.enabled = False
			.GetControl(&quot;Label1&quot;).model.enabled = False
		Else
			If FileExists(.GetControl(&quot;FileControl1&quot;).Model.Text) Then
				&apos; Festival is installed
				.GetControl(&quot;OptionButton1&quot;).model.enabled = True
				.GetControl(&quot;ComboBox1&quot;).model.enabled = .GetControl(&quot;OptionButton1&quot;).Model.State
				.GetControl(&quot;FileControl1&quot;).model.enabled = .GetControl(&quot;OptionButton1&quot;).Model.State
				.GetControl(&quot;Label1&quot;).model.enabled = .GetControl(&quot;OptionButton1&quot;).Model.State
			Else
				&apos; Cannot find festival
				.GetControl(&quot;OptionButton1&quot;).model.enabled = False
				.GetControl(&quot;ComboBox1&quot;).model.enabled = False
				.GetControl(&quot;FileControl1&quot;).model.enabled = False
				.GetControl(&quot;Label1&quot;).model.enabled = False
			End If
			
		End If
		.GetControl(&quot;ComboBox2&quot;).model.enabled = .GetControl(&quot;OptionButton2&quot;).Model.State
		.GetControl(&quot;ComboBox4&quot;).model.enabled = .GetControl(&quot;OptionButton3&quot;).Model.State
		.GetControl(&quot;FileControl2&quot;).model.enabled = .GetControl(&quot;OptionButton2&quot;).Model.State
		.GetControl(&quot;Label2&quot;).model.enabled = .GetControl(&quot;OptionButton2&quot;).Model.State
	End With
End Sub


Function fvCurrentDialogConfig() As Variant
	Dim config(fiCountConfigOptions) As Variant
	With f_dlgSpeechSetup
		config(fiAlwaysShowDialogCheckBox()) = .GetControl(&quot;CheckBox1&quot;).model.State
		config(fiSmartSelectCheckBox()) = .GetControl(&quot;CheckBox2&quot;).model.State
		config(fiDialogLanguageCombo()) = .GetControl(&quot;ComboBox3&quot;).model.Text
		config(fiExternalCommand()) = .GetControl(&quot;ComboBox2&quot;).model.Text
		config(fiExternalOption()) = .GetControl(&quot;OptionButton2&quot;).Model.State
		config(fiExternalProgramPath) = .GetControl(&quot;FileControl2&quot;).Model.Text
		config(fiFestivalComboChoice()) = .GetControl(&quot;ComboBox1&quot;).model.Text
		config(fiFestivalOption()) = .GetControl(&quot;OptionButton1&quot;).Model.State
		config(fiFestivalPath()) = .GetControl(&quot;FileControl1&quot;).Model.Text
		config(fiHtmlComboURL()) = .GetControl(&quot;ComboBox4&quot;).model.Text
		config(fiHtmlOption) = .GetControl(&quot;OptionButton3&quot;).Model.State
	End With
	fvCurrentDialogConfig() = config()
End Function


Sub aboutReadTextDialog()
	Dim config(fiCountConfigOptions) As Variant
	
	config() = fvCurrentDialogConfig()
	AboutAddOn(config(), &quot;&quot;)
End Sub


Sub AboutAddOn( _
	config() As Variant, _
	myText As String)
	Dim f_dlgAboutReadSelection As Variant
	Dim sFile As String
	Dim sA As String
	Dim msgs() As Variant
	Dim errorCode As Integer
	Dim s1$
	s1 = fsDisplayLanguage()
	Dim s2$
	s2 = fsHomeDrive()
	Dim s3$
	s3 = fsFullPathOf(&quot;ReadClipBoard.lock&quot;)
	Dim s4$
	s4 = &quot;&quot;
	
	&apos; Open up the dialog
	DialogLibraries.LoadLibrary( &quot;textToSpeech&quot; )
	f_dlgAboutReadSelection = CreateUnoDialog( DialogLibraries.textToSpeech.Dialog3)
	With f_dlgAboutReadSelection
		&apos;Update the labels And button with your language
		.model.Title = fsLookUpTerm2(&quot;s_about&quot;, s1) &amp; _
		&quot; &quot; &amp; fsLookUpTerm2(&quot;s_read-text&quot;, s1) &amp; _
		&quot; &quot; &amp; fsExtensionVersion()
		If fbHelpIsInstalled() Then
			.model.HelpURL = fsAppSignature &amp; &quot;/set_up.xhp&quot;
		End If
		.GetControl(&quot;CommandButton1&quot;).model.Label = fsLookUpTerm2(&quot;s_ok&quot;, s1)
		.GetControl(&quot;CommandButton2&quot;).model.helpText = fsLookUpTerm2(&quot;s_http-help-url&quot;, s1)
		.GetControl(&quot;CommandButton2&quot;).model.Label = fsLookUpTerm2(&quot;s_on-line-help&quot;, s1) &amp; &quot;...&quot;
		.GetControl(&quot;FrameControl1&quot;).model.Label = fsLookUpTerm2(&quot;s_about&quot;, s1)
		.GetControl(&quot;FrameControl2&quot;).model.Label = fsLookUpTerm2(&quot;s_script&quot;, s1)
		.GetControl(&quot;FrameControl3&quot;).model.Label = fsLookUpTerm2(&quot;s_command&quot;, s1)
		.GetControl(&quot;ImageControl1&quot;).model.helpText = .model.Title
		.GetControl(&quot;ImageControl1&quot;).model.ImageURL = fsExtensionLogo
		.GetControl(&quot;ImageControl2&quot;).model.helpText = fsLookUpTerm2(&quot;s_test&quot;, s1)
		.GetControl(&quot;ImageControl2&quot;).model.ImageURL = fsSpeakerLogo
		.GetControl(&quot;ImageControl3&quot;).model.helpText = Left(fsGetTextSelection(False, False),99)
		.GetControl(&quot;ImageControl3&quot;).model.ImageURL = fsSelectionLogo
		
		If fbIsWin64Program And (Not( _
			fbClipBoardAvailable) Or _
			(fsGetSetting( _
			&quot;oosetupversion&quot;) = &quot;5.0&quot; And _
			LCase( _
			fsGetSetting( _
			&quot;ooName&quot;)) = &quot;libreoffice&quot;)) Then
			.GetControl(&quot;ImageControl4&quot;).model.ImageURL = fsClipBoardLogo
			.GetControl(&quot;ImageControl4&quot;).model.Enabled = False
			.GetControl(&quot;Label2&quot;).model.Enabled = False
		Else
			CreateFile(s3, fsGetSetting(&quot;oosetupversion&quot;), &quot;UTF-8&quot;)  &apos; lock clipboard
			fsGetClipBoard(False)
			&apos; To avoid a crash, the testing version of office 
			&apos; needs a dummy run of function before evaluating
			&apos; the results.
			&apos; if it crashes, then we are left with a lock file.
			If fileexists(s3) Then  &apos; unlock clipboard
				kill s3
			End If
			s4 = fsGetClipBoard(False)
			.GetControl(&quot;ImageControl4&quot;).model.helpText = Left( _
			s4, 99)
			.GetControl(&quot;Label2&quot;).model.helpText = Left( _
			s4, 99)
			.GetControl(&quot;ImageControl4&quot;).model.ImageURL = fsClipBoardLogo
			.GetControl(&quot;Label2&quot;).model.Label = Len(s4)
		End If
		If fileexists(s2 &amp; &quot;\Windows\SysWOW64\Speech\SpeechUX\sapi.cpl&quot;) Or _
			fileexists(s2 &amp; &quot;\Windows\System32\Speech\SpeechUX\sapi.cpl&quot;) Or _
			fileexists(s2 &amp; &quot;\WINDOWS\ServicePackFiles\i386\sapi.cpl&quot;) Or _
			fileexists(fsProgramDirectory() &amp; &quot;Common Files\Microsoft Shared\Speech\sapi.cpl&quot;) Then
			.GetControl(&quot;ImageControl5&quot;).model.helpText = fsLookUpTerm2( _
			&quot;s_speech-properties&quot;, s1)
		Else
			.GetControl(&quot;ImageControl5&quot;).model.helpText = fsLookUpTerm2( _
			&quot;s_system-settings&quot;, s1)
		End If
		.GetControl(&quot;ImageControl5&quot;).model.ImageURL = fsToolBoxLogo
		.GetControl(&quot;Label1&quot;).model.helpText = Left(fsGetTextSelection(False, False),99)
		.GetControl(&quot;Label1&quot;).model.Label = Len(fsGetTextSelection(False, False))
		
		&apos;Change the text in the information text boxes
		If config(fiFestivalOption()) Then
			.GetControl(&quot;TextField3&quot;).model.Text = config(fiFestivalPath()) &amp; &quot; &quot; &amp; _
			&quot;&quot;&quot;&quot; &amp; ConvertFromURL(fsFestivalScriptName()) &amp; &quot;&quot;&quot;&quot;
			sA = replaceLocalTokensWithStr(config(fiFestivalComboChoice()))
			sA = replacePublicTokensWithStr(sA)
			.GetControl(&quot;TextField2&quot;).model.Text = sA
		ElseIf config(fiExternalOption()) and Not fbIsSandBoxed() Then
			sA = replaceLocalTokensWithStr(config(fiExternalCommand()))
			sA = replacePublicTokensWithStr(sA)
			.GetControl(&quot;TextField3&quot;).model.Text = &quot;&quot;&quot;&quot; &amp; config(fiExternalProgramPath) &amp; &quot;&quot;&quot; &quot; &amp; sA
			If InStr(config(fiExternalCommand()),&quot;(TTS_WSCRIPT_VBS)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsVBSTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(SPD_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsSPDTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(ESPEAK_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsEspeakTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(FESTIVAL_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsFestivalTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(NETWORK_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsPythonNetTtsTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(OPENJTALK_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsOpenJtalkTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(PICO_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsPicoTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(PYTTSX_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsPyttsxTextString()
			ElseIf InStr(config(fiExternalCommand()), &quot;(RHVOICE_READ_TEXT_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsRhVoiceTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(OS_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsOSTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(CREATE_QR_LABEL_PY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fsQRTextString()
			ElseIf InStr(config(fiExternalCommand()),&quot;(SAY_APPLESCRIPT)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fswriteMacOSSpeechScript(&quot;&quot;, _
				fsSelectionLanguageAndRegion(True))
			ElseIf InStr(config(fiExternalCommand()),&quot;(SAY_APPLESCRIPT_SLOWLY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fswriteMacOSSpeechScript(&quot;-r 110 &quot;, _
				fsSelectionLanguageAndRegion(True))
			ElseIf InStr(config(fiExternalCommand()),&quot;(SAY_APPLESCRIPT_QUICKLY)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fswriteMacOSSpeechScript(&quot;-r 240 &quot;, _
				fsSelectionLanguageAndRegion(True))
			ElseIf InStr(config(fiExternalCommand()),&quot;(SAY_APPLESCRIPT_SYSTEM_VOICE)&quot;) Then
				.GetControl(&quot;TextField2&quot;).model.Text = fswriteMacOSSpeechScript(&quot;&quot;, _
				&quot;&quot;)
			Else
				.GetControl(&quot;TextField2&quot;).model.Text = &quot;&quot;
			End If
		Else &apos; if config(fiHtmlOption) then
			sA = replacePublicTokensWithStr(config(fiHtmlComboURL()))
			.GetControl(&quot;TextField3&quot;).model.Text = &quot;[com.sun.star.system.SystemShellExecute]&quot;
			If InStr(sA,&quot;.google.&quot;) Then
				sA = sA &amp; Chr(13) &amp; Chr(13) &amp; &quot;Powered by Google TM&quot;
			End If
			.GetControl(&quot;TextField2&quot;).model.Text = sA
		End If
		.GetControl(&quot;TextField1&quot;).model.Text = fsAbout()
		&apos; Show the dialog
		.Execute()
	End With
End Sub

</script:module>